JOGO_MULTIPLAYER PROC ; ESTE É O PROCEDIMENTO REFERENTE AO MODO MULTIPLAYER
  MOV CX,9 ; ATRIBUI A CX O VALOR 9, NUMERO DE ESPAÇOS A SEREM PREENCHIDOS NO TABULEIRO
JOGO1: ; RÓTULO DE INICIALIZAÇÃO DO JOGO 1
  PUSH CX ; EMPILHA O ATUAL VALOR DE CX
  CLS ; EXECUTA A MACRO DE LIMPEZA DE TELA
  TEXTO_INICIO ; EXECUTA A MACRO QUE EXIBE O TEXTO 'JOGO DA VELHA'
  PULA_LINHA ; EXECUTA A MACRO QUE EXECUTA O PULO DE LINHA
  PULA_LINHA ; EXECUTA A MACRO QUE EXECUTA O PULO DE LINHA
  ORDEM_JOGS ; EXECUTA A MACRO QUE VERIFICA A VEZ DE QUAL JOGADOR
  PULA_LINHA ; EXECUTA A MACRO QUE EXECUTA O PULO DE LINHA
  PULA_LINHA ; EXECUTA A MACRO QUE EXECUTA O PULO DE LINHA
  CALL IMPRIME_MATRIZ ; EXECUTA O PROCEDIMENTO DE IMPRESSÃO DE MATRIZ
  PULA_LINHA ; EXECUTA A MACRO QUE EXECUTA O PULO DE LINHA
  CALL CAPTA_VERIFICA ; EXECUTA O PROCEDIMENTO DE CAPTAÇÃO DA ENTRADA DE DADOS E DA VERIFICAÇÃO DE VIABILIDADE DE ALOCAR O SÍMBOLO NA POSIÇÃO DESEJADA
  PULA_LINHA ; EXECUTA A MACRO QUE EXECUTA O PULO DE LINHA
  CMP AX,1 
  JE SAI_DEFINITIVO
  CMP AX,2
  JE SAI_DEFINITIVO
  POP CX
  LOOP JOGO1
  CALL IMPRIME_MATRIZ ; EXECUTA O PROCEDIMENTO DE IMPRESSÃO DE MATRIZ
  RET
SAI_DEFINITIVO:
  POP CX
  CALL IMPRIME_MATRIZ ; EXECUTA O PROCEDIMENTO DE IMPRESSÃO DE MATRIZ
  RET
 JOGO_MULTIPLAYER ENDP

 JOGO_ROBO PROC
  RET
 JOGO_ROBO ENDP

  CAPTA_VERIFICA PROC ; PROCEDIMENTO DE CAPTAÇÃO DA ENTRADA DE DADOS E DE VERIFICAÇÃO DE VIABILIDADE DE ALOCAR O SÍMBOLO NA POSIÇÃO DESEJADA
VOLTA:
  MOV AH,9
  LEA DX,MSG_CAPTA_JOG
  INT 21h

  MOV AH,1 ; FUNÇÃO DE CAPTAÇÃO DE ENTRADA DE CARACTERES
  INT 21h
    
  XOR BX,BX ; LIMPA BX
  XOR SI,SI ; LIMPA SI

  AND AL,0Fh ; CONVERTE O CARACTERE QUE FOI DIGITADO PARA NUMERAL

  TEST AL,15 ; SE O CARACTERE DIGITADO FOR INVÁLIDO, RETORNE UM ERRO
  JZ ERRO

  CMP AL,3 ; SE A POSIÇÃO DE ENTRADA FOR MENOR OU IGUAL A 3, VÁ PARA O RÓTULO1 (INSERÇÃO DO SÍMBOLO NA PRIMEIRA LINHA DO TABULEIRO)
  JBE ROT1
  CMP AL,6 ; SE A POSIÇÃO DE ENTRADA FOR MENOR OU IGUAL A 6, VÁ PARA O RÓTULO2 (INSERÇÃO DO SÍMBOLO NA SEGUNDA LINHA DO TABULEIRO)
  JBE ROT2
  CMP AL,9 ; SE A POSIÇÃO DE ENTRADA FOR MENOR OU IGUAL A 9, VÁ PARA O RÓTULO3 (INSERÇÃO DO SÍMBOLO NA TERCEIRA LINHA DO TABULEIRO)
  JBE ROT3

ERRO:
  TENTE_NOVAMENTE ; RETORNE UM ERRO SE A ENTRADA FOR INVÁLIDA
  JMP VOLTA

ROT1:
  MOV BL,AL ; ATRIBUA A BL O VALOR CONTIDO EM AL
  DEC BL ; DECREMENTE AL PARA POSSIBILITAR A INSERÇÃO DO SÍMBOLO NO ESPAÇO ESPECIFICADO
  MOV SI,BX ; MOVIMENTE O VALOR DE BX A SI
  MOV BL,0 ; ATRIBUA 0 A BL (PRIMEIRA LINHA)
  JMP TEST1 ; VÁ PARA O RÓTULO TEST1
ROT2:
  MOV BL,AL ; ATRIBUA A BL O VALOR CONTIDO EM AL
  SUB BL,4 ; DECREMENTE AL EM 4 PARA POSSIBILITAR A INSERÇÃO DO SÍMBOLO NO ESPAÇO ESPECIFICADO
  MOV SI,BX ; MOVIMENTE O VALOR DE BX A SI
  MOV BL,3 ; ATRIBUA 3 A BL (SEGUNDA LINHA)
  JMP TEST1 ; VÁ PARA O RÓTULO TEST1
ROT3:
  MOV BL,AL ; ATRIBUA A BL O VALOR CONTIDO EM AL
  SUB BL,7 ; DECREMENTE AL EM 7 PARA POSSIBILITAR A INSERÇÃO DO SÍMBOLO NO ESPAÇO ESPECIFICADO
  MOV SI,BX ; MOVIMENTE O VALOR DE BX A SI
  MOV BL,6 ; ATRIBUA 6 A BL (SEGUNDA LINHA)

TEST1:
  CALL VERIFICA_MATRIZ ; ; VÁ PARA O PROCEDIMENTO QUE VERIFICA SE O ESPAÇO EM QUE O USUÁRIO QUER PREENCHER JÁ ESTÁ OCUPADO OU SE A POSIÇÃO EM QUE USUÁRIO QUER POSICIONAR A SUA JOGADA É INVÁLIDA

  TEST AX,1 ; SE NÃO ESTIVER PREENCHIDO VÁ PARA CONT_MAT
  JZ CONT_MAT ; E VERIFIQUE SE UM DOS JOGADORES GANHOU

  TENTE_NOVAMENTE ; EXECUTE A MACRO QUE IMPRIME A MENSAGEM DE IMPRESSÃO 
  JMP VOLTA

CONT_MAT:
  CALL INSERE_MATRIZ ; EXECUTA O PROCEDIMENTO DE IMPRESSÃO DE MATRIZ
  CALL VER_VIT ; EXECUTA O PROCEDIMENTO DE VERIFICAÇÃO DE VITÓRIA
  CMP AX,0 ; SE NINGUÉM VENCEU, FINALIZE O PROCEDIMENTO
  JE FINALIZA
  CMP AX,1 ; SE O JOGADOR 1 VENCEU, VÁ PARA O RÓTULO DE VITÓRIA DO JOGADOR 1
  JE VIT_1
  CMP AX,2 ; SE O JOGADOR 2 VENCEU, VÁ PARA O RÓTULO DE VITÓRIA DO JOGADOR 2
  JE VIT_2

VIT_1:
  CLS ; EXECUTA A MACRO DE LIMPEZA DE TELA
  MSG_VIT_1 ; IMPRIMA A MENSAGEM DE VITÓRIA DO JOGADOR 1
  JMP FINALIZA
VIT_2:
  CLS ; EXECUTA A MACRO DE LIMPEZA DE TELA
  MSG_VIT_2 ; IMPRIMA A MENSAGEM DE VITÓRIA DO JOGADOR 2

FINALIZA:
  RET
 CAPTA_VERIFICA ENDP

 INSERE_MATRIZ PROC ; ESTE PROCEDIMENTO INSERE NA MATRIZ O SÍMBOLO A SER ALOCADO EM UMA POSIÇÃO DEPENDENDO DA VEZ DO JOGADOR
  TEST CX,1 ; SE A VEZ FOR DO JOGADOR 1 VÁ PARA JOG1 E INSIRA 'X'
  JNZ JOG1 ; SE A VEZ FOR DO JOGADOR 2 INSIRA 'O'
  MOV MATRIZ[BX][SI],6Fh
  JMP FINAL ; SAIA DO PROCEDIMENTO

JOG1:
  MOV MATRIZ[BX][SI],78h

FINAL: 
  RET
 INSERE_MATRIZ ENDP

 VERIFICA_MATRIZ PROC ; ESTE PROCEDIMENTO VERIFICA SE O ESPAÇO EM QUE O USUÁRIO QUER PREENCHER JÁ ESTÁ OCUPADO OU SE A POSIÇÃO EM QUE USUÁRIO QUER POSICIONAR A SUA JOGADA É INVÁLIDA
  XOR AX,AX

  CMP MATRIZ[BX][SI],78h ; VERIFICA SE O ATUAL ESPAÇO É PREENCHIDO COM 'X'
  JZ SAI8 ; SE JÁ ESTIVER OCUPADO RETORNE UM ERRO
  CMP MATRIZ[BX][SI],6Fh ; VERIFICA SE O ATUAL ESPAÇO É PREENCHIDO COM 'O'
  JZ SAI8 ; SE JÁ ESTIVER OCUPADO RETORNE UM ERRO
  ; SE NÃO ESTIVER OCUPADO, SIGA O FLUXO DO PROGRAMA
  CMP MATRIZ[BX][SI],31h
  JAE CONTINUA 
  CMP MATRIZ[BX][SI],39h
  JBE CONTINUA

SAI8:
  MOV AX,1

CONTINUA:
  RET
 VERIFICA_MATRIZ ENDP

 TENTE_NOVAMENTE PROC
  PUSH AX ; EMPILHA OS VALORES ATUAIS DE AX E DE DX
  PUSH DX

  MOV AH,9
  LEA DX,MSG_INVALIDO
  INT 21h

  CALL IMPRIME_MATRIZ ; EXECUTA O PROCEDIMENTO DE IMPRESSÃO DE MATRIZ
  
  POP DX ;DESEMPILHA E ARMAZENA OS VALORES ANTERIORMENTE EMPILHADOS EM DX E AX, RESPECTIVAMENTE
    POP AX
  RET
 TENTE_NOVAMENTE ENDP

 IMPRIME_MATRIZ PROC 
 ; ESTE PROCEDIMENTO IMPRIME A MATRIZ EM SEU ESTADO ATUAL, TRATA-SE DE UM PROCEDIMENTO VISUAL PARA O PROGRAMA
 ; ENTRADA: MATRIZ 3X3
 ; SAÍDA: EXIBIÇÃO NA TELA DA MATRIZ 3X3
  PUSH AX ; EMPILHA OS VALORES ATUAIS DOS SEGUINTES REGISTRADORES
  PUSH BX
  PUSH CX
  PUSH DX
  XOR BX,BX ; LIMPA BX
  MOV AH,2 ; INICIALIZA A FUNÇÃO DE IMPRESSÃO DE CARACTERES
  MOV CH,3 ; ATRIBUI A CH O VALOR 3

REPETE2:
  MOV CL,3 ; ATRIBUI A CL O VALOR 3
  XOR SI,SI
  MOV DL,20h
  INT 21h
REPETE1:
  MOV AL,MATRIZ[BX][SI]

  CMP AL,6Fh
  JE IMPRIME0

  CMP AL,78h
  JE IMPRIMEX

  JMP NUMERO

IMPRIME0:
  MOV DL,6Fh
  INT 21h
  JMP COND

IMPRIMEX:
  MOV DL,78h
  INT 21h
  JMP COND

NUMERO:
  MOV DL,AL
  INT 21H
  JMP COND

COND:
  CMP CL,1
  JE FINAL1
  MOV DL,7Ch
  INT 21h  

FINAL1:
  INC SI
  DEC CL
  JNZ REPETE1
  ADD BX,3

  CMP CH,1
  JE FINAL2

  MOV DL,10
  INT 21h

  MOV DL,20h
  INT 21h

  MOV DH,3
DENOVO:
  MOV DL,2Dh
  INT 21h

  CMP DH,1
  JE FINAL2

  MOV DL,7Ch
  INT 21h
  DEC DH
  JNZ DENOVO

FINAL2:
  MOV DL,10
  INT 21h
  DEC CH
  JNZ REPETE2

  POP DX
  POP CX
  POP BX
  POP AX
  RET
 IMPRIME_MATRIZ ENDP

 IMPRIME_UM PROC
  MOV AH, 09H
  MOV DX, OFFSET MSG_UM
  INT 21H

  RET
 IMPRIME_UM ENDP

 INICIALIZACAO PROC
 ; PUSHALL

  ; IMPRIME MENSAGEM 'JOGO DA VELHA'
  MOV AH, 09H
  MOV DX, OFFSET MSG_BEMVINDO
  INT 21H

  PULA_LINHA ; EXECUTA A MACRO QUE EXECUTA O PULO DE LINHA ; EXECUTA A MACRO DE PULAR LINHA
  PULA_LINHA ; EXECUTA A MACRO QUE EXECUTA O PULO DE LINHA ; EXECUTA A MACRO DE PULAR LINHA

ENTRADA_OPCAO:
  ; IMPRIME MENSAGEM DE MODO DE JOGO
  MOV DX, OFFSET MSG1
  INT 21H

  MOV AH, 01H ; EXECUTA A FUNÇÃO DE LEITURA DE CARACTERE (ENTRADA DO USUÁRIO) PARA SABER QUAL A OPÇÃO A SER DIGITADA
  INT 21H

  AND AL, 0FH ; TRANSFORMA O CARACTERE DIGITADO EM NUMERAL

  CMP AL, 0 
  JZ VOLTAR
  CMP AL, 1
  JE VOLTAR
  ; SE A OPÇÃO DIGITADA FOR 0 OU 1, RETORNE AO PROGRAMA PRINCIPAL
  ; SE HOUVER A DETECÇÃO DE UMA ENTRADA DIFERENTE DE UM DOS NÚMEROS ACIMA, IMPRIMA UMA MENSAGEM DE ENTRADA INVÁLIDA
  MOV AH, 09H
  MOV DX, OFFSET MSG2
  INT 21H
  JMP ENTRADA_OPCAO

VOLTAR:; RÓTULO DE RETORNO AO PROGRAMA PRINCIPAL
  RET 
 INICIALIZACAO ENDP

VER_VIT PROC ; ESTE PROCEDIMENTO TEM COMO OBJETIVO VERIFICAR A VITÓRIA
 ;Verificação horizontal
  XOR BX,BX ; LIMPA O REGISTRADOR BX
  MOV CL,3 ; ATRIBUI AO REGISTRADOR CL O VALOR 3

DENOVO1:
  XOR DX,DX ; LIMPA O REGITRADOR DX
  XOR SI,SI ; LIMPA O REGISTRADOR SI
  MOV CH,3 ; ATRIBUI AO REGISTRADOR CH O VALOR 3

AGAIN1:
  CMP MATRIZ[BX][SI],78h ; VERIFICA SE O ATUAL ESPAÇO É PREENCHIDO COM 'X' ; VERIFICA SE O ATUAL ESPAÇO É PREENCHIDO COM 'X'
  JNE CONT1 ; SE NÃO FOI IGUAL VERIFIQUE SE É 'O'
  INC DH ; SE FOR IGUAL A 'X', INCREMENTE O HIGH BYTE DE DX
CONT1:
  CMP MATRIZ[BX][SI],6Fh ; VERIFICA SE O ATUAL ESPAÇO É PREENCHIDO COM 'O' ; VERIFICA SE O ATUAL ESPAÇO É PREENCHIDO COM 'O'
  JNE SAI1 ; SE NÃO FOI IGUAL ENTÃO O ESPEÇO ESTÁ 'VAZIO'
  INC DL ; SE FOR IGUAL A 'O', INCREMENTE O LOW BYTE DE DX
SAI1: 
  INC SI ; PASSA PARA A PRÓXIMA COLUNA
  DEC CH ; DECREMENTA O NÚMERO DE ESPAÇOS DA LINHA A SER VERIFICADO 
  JNZ AGAIN1 ; ENQUANTO HOUVER ESPAÇOS A SEREM VERIFICADOS, REPITA O RÓTULO AGAIN1

  CMP DH,3 ; HÁ 3 'X' ALINHADOS ?
  JE CORRECTION1 ; SE SIM, ATRIBUA A VITÓRIA AO JOGADOR 1
  CMP DL,3 ; HÁ 3 'O' ALINHADOS ?
  JE CORRECTION2 ; SE SIM, ATRIBUA A VITÓRIA AO JOGADOR 2 / COMPUTADOR
  ADD BX,3 ; PASSA PARA A PRÓXIMA LINHA
  DEC CL ; SE NÃO VENCEDOR NESSA LINHA, VERIFIQUE NA PRÓXIMA LINHA
  JNZ DENOVO1

  ;Verificação vertical
  XOR SI,SI ; LIMPA O REGISTRADOR SI
  MOV CL,3 ; ATRIBUI A CL O VALOR 3
DENOVO2:
  XOR DX,DX ; LIMPA DX
  XOR BX,BX ; LIMPA BX
  MOV CH,3 ; ATRIBUI A CH O VALOR 3
AGAIN2:
  CMP MATRIZ[BX][SI],78h ; VERIFICA SE O ATUAL ESPAÇO É PREENCHIDO COM 'X' ; VERIFICA SE O ATUAL ESPAÇO É PREENCHIDO COM 'X'
  JNE CONT2 ; SE NÃO FOI IGUAL VERIFIQUE SE É 'O'
  INC DH ; SE FOR IGUAL A 'X', INCREMENTE O HIGH BYTE DE DX
CONT2:
  CMP MATRIZ[BX][SI],6Fh ; VERIFICA SE O ATUAL ESPAÇO É PREENCHIDO COM 'O' ; VERIFICA SE O ATUAL ESPAÇO É PREENCHIDO COM 'O'
  JNE SAI12 ; SE NÃO FOI IGUAL ENTÃO O ESPEÇO ESTÁ 'VAZIO'
  INC DL ; SE FOR IGUAL A 'O', INCREMENTE O LOW BYTE DE DX
SAI12:
  ADD BX,3 ; PASSA A PRÓXIMA LINHA
  DEC CH ; DECREMENTA O NÚMERO DE ESPAÇOS DA COLUNA A SER VERIFICADO 
  JNZ AGAIN2 ; ENQUANTO HOUVER ESPAÇOS A SEREM VERIFICADOS, REPITA O RÓTULO AGAIN2

  CMP DH,3 ; HÁ 3 'X' ALINHADOS ?
CORRECTION1:
  JE VIT_JOG ; SE SIM, ATRIBUA A VITÓRIA AO JOGADOR 1
  CMP DL,3 ; HÁ 3 'O' ALINHADOS ?
CORRECTION2:
  JE VIT_JOG2_COMP ; SE SIM, ATRIBUA A VITÓRIA AO JOGADOR 2 / COMPUTADOR
  INC SI ; PASSE PARA A PRÓXIMA COLUNA
  DEC CL ; DECREMENTA CL
  JNZ DENOVO2 ; SE AINDA HOUVER COLUNAS A SEREM VERIFICADAS REPITA O RÓTULO DENOVO2
      
  ; verificação diagonal principal    
  XOR SI,SI ; LIMPA O REGISTRADOR SI
  XOR BX,BX ; LIMPA BX
  XOR DX,DX ; LIMPA DX
  MOV CX,3 ; ATRIBUI A CX O VALOR 3
AGAIN3:
  CMP MATRIZ[BX][SI],78h ; VERIFICA SE O ATUAL ESPAÇO É PREENCHIDO COM 'X'
  JNE CONT3 ; SE NÃO VERIFIQUE SE É IGUAL A 'O'
  INC DH ; INCREMENTA DH 
CONT3:
  CMP MATRIZ[BX][SI],6Fh ; VERIFICA SE O ATUAL ESPAÇO É PREENCHIDO COM 'O'
  JNE SAI3 ; SE NÃO VERIFIQUE ENTÃO O ESPAÇO ESTÁ 'VAZIO'
  INC DL ; INCREMENTA DL
SAI3:
  ADD BX,3 ; PASSA PARA A PRÓXIMA LINHA
  INC SI ; VAI UMA COLUNA PARA FRENTE
  LOOP AGAIN3 ; ENQUANTO TODA A DIAGONAL NÃO FOR LIDA, VOLTE AO RÓTULO AGAIN4
  CMP DH,3 ; SE HOUVER 3 'X', ALINHADOS ATRIBUA A VITÓRIA AO JOGADOR 1
  JE VIT_JOG
  CMP DL,3 ; SE HOUVER 3 'O', ALINHADOS ATRIBUA A VITÓRIA AO JOGADOR 2
  JE VIT_JOG2_COMP
 
 ; verificação diagonal secundária
  XOR DX,DX ; LIMPA O REGISTRADOR DX
  XOR BX,BX ; LIMPA O REGISTRADOR BX
  MOV SI,2 ; ATRIBUI A INDEXAÇÃO DA ÚLTIMA COLUNA
  MOV CX,3 ; ATRIBUI A CX O VALOR 3
AGAIN4:
  CMP MATRIZ[BX][SI],78h ; VERIFICA SE O ATUAL ESPAÇO É PREENCHIDO COM 'X'
  JNE CONT4 ; SE NÃO VERIFIQUE SE É IGUAL A 'O'
  INC DH ; INCREMENTA DH 
CONT4:
  CMP MATRIZ[BX][SI],6Fh ; VERIFICA SE O ATUAL ESPAÇO É PREENCHIDO COM 'O'
  JNE SAI4 ; SE NÃO VERIFIQUE ENTÃO O ESPAÇO ESTÁ 'VAZIO'
  INC DL ; INCREMENTA DL
SAI4:
  ADD BX,3 ; PASSA PARA A PRÓXIMA LINHA
  DEC SI ; VAI UMA COLUNA PARA TRÁS
  LOOP AGAIN4 ; ENQUANTO TODA A DIAGONAL NÃO FOR LIDA, VOLTE AO RÓTULO AGAIN4
  CMP DH,3 ; SE HOUVER 3 'X', ALINHADOS ATRIBUA A VITÓRIA AO JOGADOR 1
  JE VIT_JOG
  CMP DL,3 ; SE HOUVER 3 'O', ALINHADOS ATRIBUA A VITÓRIA AO JOGADOR 2
  JE VIT_JOG2_COMP
  JMP RETORNA
VIT_JOG: ; RÓTULO DE ATRIBUIÇÃO DE VITÓRIA AO JOGADOR 1
  CLS ; EXECUTA A MACRO DE LIMPEZA DE TELA
  MOV AX,1 ; ATRIBUI A AX O VALOR 1 (JOGADOR 1 FOI O VENCEDOR)
  JMP FINAL_VIT ; VAI PARA O RÓTULO DE RETORNO
VIT_JOG2_COMP:; RÓTULO DE ATRIBUIÇÃO DE VITÓRIA AO JOGADOR 2 (OU COMPUTADOR)
  CLS ; EXECUTA A MACRO DE LIMPEZA DE TELA
  MOV AX,2 ; ATRIBUI A AX O VALOR 2 (JOGADOR 2 OU COMPUTADOR VENCEU)
  JMP FINAL_VIT ; VAI PARA O RÓTULO DE RETORNO
RETORNA:
  MOV AX,0 ; AINDA NÃO HOUVE UM JOGADOR VENCEDOR
FINAL_VIT:
  RET
 VER_VIT ENDP

 RESET_MATRIZ PROC ; PROCEDIMENTO QUE RETORNA O TABULEIRO AO SEU ESTADO ORIGINAL (COMO DECLARADO NO .DATA)
  PUSH BX ; EMPILHA OS VALORES ATUAIS DE BX E DE SI, RESPECTIVAMENTE
  PUSH SI

  XOR CX,CX ; LIMPA O REGISTRADOR CX
  XOR BX,BX ; LIMPA O REGISTRADOR BX
  MOV DL,31h ; ATRIBUI A CL O CARACTERE '1'
  MOV CH,3 ; ATRIBUI A CH O VALOR 3 (LINHAS DA MATRIZ A SEREM RESETADAS)

RESET_MAT2:
  XOR SI,SI ; LIMPA O REGISTRADOR SI
  MOV CL,3 ; ATRIBUI A CL O VALOR 3 (COLUNAS DA ATUAL LINHA DA MATRIZ QUE FALTAM A SEREM RESETADAS)
RESET_MAT1:
  MOV MATRIZ[BX][SI],DL ; MOVIMENTAÇÃO DO ATUAL CARACTERE DE DL À POSIÇÃO DA MATRIZ DEFINIDA POR BX E SI
  INC DL ; INCREMENTA O VALOR DE DL PARA O PRÓXIMO CARACTERE A SER LIDO
  INC SI ; PASSA PARA A PRÓXIMA LINHA A SER PREENCHIDA DA ATUAL LINHA
  DEC CL ; DECREMENTA A QUANTIDADE DE COLUNAS DA ATUAL LINHA QUE RESTAM A SER PREENCHIDAS
  JNZ RESET_MAT1 ; ENQUANTO HOUVER COLUNAS A SEREM PREENCHIDAS VOLTE AO INÍCIO DO RÓTULO RESET_MAT1
  ADD BX,3 ; MOVE PARA A PRÓXIMA LINHA DA MATRIZ
  DEC CH ; DECREMENTA O NÚMERO DE LINHAS A SEREM PREENCHIDAS
  JNZ RESET_MAT2 ; ENQUANTO HOUVER LINHAS A SEREM PREENCHIDAS, VOLTE AO RÓTULO RESET_MAT2

  POP SI ; ARMAZENA OS DOIS VALORES DO TOPO DA PILHA EM SI E EM BX, RESPECTIVAMENTE.
  POP BX
  RET ; FIM DO PROCEDIMENTO
 RESET_MATRIZ ENDP
 